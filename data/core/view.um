import (
	"env.um"
	"renderer.um"
	"style.um"
	"common.um"
	"config.um"
	"cex.um"
)

fn move_towards*(val: ^real, dest: real, rate: real = 0.5) {
	res := val^
	if fabs(val^ - dest) < 0.5 {
		res = dest
	} else {
		res = common::flerp(val^, dest, rate)
	}
	if val^ != res {
		cex::redraw = true
	}
	val^ = res
}

// sz = get_scrollbar_size
fn get_scrollbar_rect*(sz: real, position, size: common::Vec2, scroll: common::Scroll): common::Rect {
	if sz <= size.y || sz == env::INT_MAX {
		return {}
	}
	h := size.y * size.y / sz
	h = (h > 20.0)? h : 20.0
	return {
    	x: position.x + size.x - style::scrollbar_size,
    	y: position.y + scroll.y * (size.y - h) / (sz - size.y),
    	w: style::scrollbar_size,
	    h: h,
	}
}

// s := get_scrollbar_rect(self)
fn scrollbar_overlaps_point*(s: common::Rect, x, y: real): bool {
	return x >= s.x - s.w * 3.0 && x < s.x + s.w && y >= s.y && y < s.y + s.h
}

// s := get_scrollbar_rect(self)
// dragging = &self.dragging
fn on_mouse_pressed*(s: common::Rect, dragging: ^bool, x, y: real): bool {
	if scrollbar_overlaps_point(s, x, y) {
		dragging^ = true
		return true
	}
	return false
}

fn on_mouse_released*(dragging: ^bool) {
	dragging^ = false
}

fn on_mouse_moved*(hovered, dragging: ^bool, position, size: common::Vec2, scroll: ^common::Scroll, sz,x,y,dx,dy: real) {
	if dragging^ {
		delta := sz / size.y*dy
		scroll.to.y = scroll.to.y + delta
	}
	s := get_scrollbar_rect(sz, position, size, scroll^)
	hovered^ = scrollbar_overlaps_point(s, x,y)
}

fn on_mouse_wheel*(scrollable: bool, scroll: ^common::Scroll, y: real) {
	if scrollable {
	    scroll.to.y = scroll.to.y + y * (-config::mouse_wheel_scroll)
	}
}

fn get_content_bounds*(size: common::Vec2, scroll: common::Scroll): common::Rect {
	x := scroll.x
	y := scroll.y
	return {x, y, x + size.x, y + size.y}
}

fn get_content_offset*(position: common::Vec2, scroll: common::Scroll): (real, real) {
	x := round(position.x - scroll.x)
	y := round(position.y - scroll.y)
	return x, y
}

fn update*(sz: real, size: common::Vec2, scroll: ^common::Scroll) {
	max := sz - size.y
	scroll.to.y = common::fclamp(scroll.to.y, 0, max)
	move_towards(&scroll.x, scroll.to.x, 0.3)
	move_towards(&scroll.y, scroll.to.y, 0.3)
}

fn draw_background*(position, size: common::Vec2, color: renderer::Color) {
	x, y := position.x, position.y
	w, h := size.x, size.y
	renderer::fdraw_rect(x, y, w + x % 1, h + y % 1, color)
}

// s := get_scrollbar_rect(self)
fn draw_scrollbar*(s: common::Rect, hovered, dragging: bool) {
	highlight := hovered || dragging
	color := (highlight)? style::scrollbar2 : style::scrollbar
	renderer::fdraw_rect(s.x, s.y, s.w, s.h, color)
}

